plugins {
    id 'java-library'
    id 'org.springframework.boot' apply false
    id 'io.spring.dependency-management' apply false
    id 'com.diffplug.spotless' version "7.0.2" apply false
}

allprojects {
    group = "${projectGroup}"
    version = "${applicationVersion}"

    java {
        sourceCompatibility = project.javaVersion
        targetCompatibility = project.javaVersion
    }

    repositories {
        mavenCentral()
    }
}

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.diffplug.spotless'

    bootJar.enabled = false // 실행가능한 jar
    jar.enabled = true // plain jar

    dependencies {
        // lombok
        compileOnly 'org.projectlombok:lombok'          // 컴파일 시점에만 사용
        annotationProcessor 'org.projectlombok:lombok'  // 코드 생성을 위해 사용

        // teset
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    // compileOnly가 annotationProcessor를 상속하도록 설정
    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    spotless {
        java {
            importOrder()

            removeUnusedImports()
            cleanthat()

            googleJavaFormat().aosp().reflowLongStrings().formatJavadoc(false).reorderImports(false).groupArtifact('com.google.googlejavaformat:google-java-format')

            formatAnnotations()  // fixes formatting of type annotations, see below
        }
    }
}

